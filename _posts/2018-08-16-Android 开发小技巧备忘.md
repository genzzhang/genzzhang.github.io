---
layout: post
title: Android 开发小技巧备忘
date: 2018-08-16 21:30
categories: Android
tags: 技巧
---

window中，在当前目录，**shift+右击**，选择在此处打开命令窗口，直接调出cmd。
adb命令运行的文件在android-sdk\platform-tools中，如下：
> adb.exe   
> AdbWinApi.dll  
> AdbWinUsbApi.dll


## 1、动态dump   
### 1) Native C++版本
```   
#include <utils/CallStack.h>然后使用CallStack类
```  
### 2) Java版本
```   
Exception e = new Exception("dumpinfo");  
e.printStackTrace()
```   


* **.bat打印log到当前目录文件**
```
cd  %~dp0
@echo off
echo 正在检查设备连接...
for /f "skip=1 tokens=1" %%i in ('adb devices') do set d=%%i
if "%d%"=="" (
    echo 未检测到设备连接 & pause
    exit
)
echo 设备%d%已连接，开始采集log，操作手机重现bug后按ctrl+c结束
set logTitle=%date:~0,4%%date:~5,2%%date:~8,2%_%time:~0,2%%time:~3,2%%time:~6,2%_%time:~9,2%
adb logcat -v time > log_%logTitle%.txt  
``` 
* **查看running activities**
```  
adb shell dumpsys activity activities | sed -ne '/Running activities/,/Run #0/p'
```  
* **查看top activity**
```  
adb shell dumpsys activity top
``` 
* **查看内存**
```  
adb shell dumpsys meminfo com.tentcent.qqpimsecure
``` 
* **startActivity/Broadcast**
```  
adb shell am start -n "com.tencent.qqpimsecure/com.tencent.server.fore.QuickLoadActivity" 
-a android.intent.action.MAIN -c android.intent.category.LAUNCHER

adb shell am broadcast -a xxx
``` 
* **ANR**，/data/anr/traces.txt,一般直接进入anr目录不需要权限
```  
adb shell
su
cd /data/anr
ls
cat traces.txt > /sdcard/traces.txt
exit
adb pull /sdcard/traces.txt .
``` 

## 2、文件操作    
一些常用文件操作adb命令：

* **截图**
```  
rem 截图
cd  %~dp0
@echo off
title 截图
color 0A
adb wait-for-device
set picTitle=%date:~0,4%%date:~5,2%%date:~8,2%_%time:~0,2%%time:~3,2%%time:~6,2%_%time:~9,2%
adb shell screencap -p | sed 's/\r$//' > %picTitle%.png
echo.
```  
* sd卡导入到电脑当前文件夹
``` 
adb pull /sdcard/log.txt .
``` 
* 电脑文件导入到sd卡目录
``` 
adb push log.txt /sdcard/
``` 
* 电脑文件导入到sd卡目录
``` 
adb push log.txt /sdcard/
```    

##  3、安装apk，直接将文件拖到.bat命令上安装
```  
rem 直接将文件拖到这个命令上即可安装
@echo off
title 安装手机软件
color 0A
echo 正准备安装
echo "%~f1"
cd "%~dp0"
cd ..
adb wait-for-device
adb install -r "%~f1"
echo 约5秒后自动退出
ping -n 5 127.1 > nul
exit
```   

## 4、卸载apk  
```   
rem 卸载手机软件，输入1 2 3 或者包名即可
@echo off
title 卸载手机软件
color 0A

:start
cls
echo ---------------请输入卸载的选择项---------------
echo ==1==    写入1或等待4秒，默认手机管家
echo ==2==    写入2，system,列出包名，copy
echo ==3==    写入3，data，由于权限，估计弄不出来
echo ==4==    写入4，输入包名
echo.

choice /C 1234 /D 1 /T 4 /M "请选择"
if %errorlevel%==1 (
    set apktype=com.tencent.qqpimsecure 
    goto uninstall) else if %errorlevel%==2 (
    set apktype=system
    goto applist) else if %errorlevel%==3 (
    set apktype=data
    goto applist) else (
    SET /P apktype=请输入包名后回车:
    goto uninstall)

:applist
cls
echo %apktype%/app文件列表
echo ----------------------------------
adb shell ls %apktype%/app
echo ----------------------------------
set /P INPUT=请输入包名:
set apktype=%input%
goto uninstall

:uninstall
cls
echo 准备卸载%apktype%
adb uninstall %apktype%
echo 约6秒后自动退出
ping -n 6 127.1 > nul
exit  
```   